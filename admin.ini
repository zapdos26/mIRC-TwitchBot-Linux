[script]
n0=on *:text:!streamer*:#:{
n1=  if ($msgtags(mod).key == 1 || $right(#,-1) == $nick) {
n2=    if (streamertwitchname == $2) {
n3=      set %streamer. [ $+ [ # ] $+ ] .streamertwitchname  $3
n4=      msg # Streamer twitch name has been set.
n5=    }
n6=    elseif (raid == $2) {
n7=      if ((@raidtarget@ !isin $3-) && (@link@ isin $3-)) { return }
n8=      else { 
n9=        set %streamer. [ $+ [ # ] $+ ] .raid $3- 
n10=        msg # Raid Channel Message has been set.
n11=      }
n12=    } 
n13=    elseif (multi == $2) {
n14=      if ((Moderator == $3) && ($4 == On || $4 == Off)) {
n15=        set %streamer. [ $+ [ # ] $+ ] .multi.mod $4
n16=        msg # The Moderator Only !multi has been set to $4 $+ .
n17=      }
n18=      elseif (@multitarget@ !isin $3-) { return }
n19=      else {
n20=        set %streamer. [ $+ [ # ] $+ ] .multi $3-
n21=        msg # Multi message has been set. 
n22=      }
n23=    }
n24=    elseif (sub == $2) {
n25=      if (@sub@ !isin $3-) { return }
n26=      else {
n27=        set %streamer. [ $+ [ # ] $+ ] .sub $3-
n28=        msg # Sub message has been set. 
n29=      }
n30=    }
n31=    elseif (resub == $2) {
n32=      if ((@sub@ !isin $3-) || (@month@ !isin $3-))  { return }
n33=      else {
n34=        set %streamer. [ $+ [ # ] $+ ] .resub $3-
n35=        msg # Resub message has been set. 
n36=      }
n37=    }
n38=    elseif (uptime == $2) {
n39=      if (@uptime@ !isin $3-) { return }
n40=      else { 
n41=        set %streamer. [ $+ [ # ] $+ ] .uptime.now $3-
n42=        msg # Uptime message has been set
n43=      }
n44=    }
n45=    elseif (offline == $2) {
n46=      set %streamer. [ $+ [ # ] $+ ] .uptime.offline $3- 
n47=      msg # Offline message has been set.
n48=    }
n49=    elseif (bits == $2) {
n50=      if (message == $3) {
n51=        set %streamer. [ $+ [ # ] $+ ] .bits.message $4- 
n52=        msg # Bits message has been set. 
n53=      }
n54=      elseif ((amount == $3) && ($4 isnum)) {
n55=        set %streamer. [ $+ [ # ] $+ ] .bits.amount $4
n56=        msg # Minimum cheer amount has been set. 
n57=      }
n58=    }
n59=    elseif (shoutout == $2) {
n60=      if ($3 == command) {
n61=        set %streamer. [ $+ [ # ] $+ ] .shoutout.command $4
n62=        msg # Shoutout command has been set.
n63=      }
n64=      elseif ($3 == message) { 
n65=        set %streamer. [ $+ [ # ] $+ ] .shoutout.message $4-
n66=        msg # Shoutout message has been set.
n67=      }
n68=    }
n69=    else { msg # $nick $+ , please check out [link] for info on how to use !streamer. }
n70=  }
n71=}
n72=
n73=
n74=on *:text:!multi*:#:{
n75=  if ($msgtags(mod).key == 1 || $right(#,-1) == $nick) {
n76=    if ($2 == set) {
n77=      set %multi. [ $+ [ # ] ] %streamer. [ $+ [ # ] $+ ] .multi
n78=      set %multi. [ $+ [ # ] ] $replace(%multi. [ $+ [ # ] ],@multitarget@,$3)
n79=      /timermulti [ $+ [ # ] ] 0 300 msg # %multi. [ $+ [ # ] ]
n80=      msg # Multistreaming with $3 has been turned on.
n81=      halt
n82=    }
n83=    elseif ( $2 == off) {
n84=      /timermulti [ $+ [ # ] ] off
n85=      msg # Multistreaming has been turned off. 
n86=      unset %multi. [ $+ [ # ] ]
n87=      halt
n88=    }
n89=  }
n90=  elseif (($msgtags(mod).key != 1) && (%streamer. [ $+ [ # ] $+ ] .multi.mod == On)) { halt }
n91=  else {
n92=    msg # %multi. [ $+ [ # ] ]
n93=    /timermulti [ $+ [ # ] ] 0 300 msg # %multi. [ $+ [ # ] ]
n94=  }
n95=}
n96=
n97=
n98=on *:TEXT:!raid *:#: {
n99=  if (($msgtags(mod).key != 1) && ($right(#,-1) != $nick)) { return } 
n100=  if ($$0 < 2) { msg $chan Wrong! Incorrect! Use: !raid [channel name] }
n101=  elseif (twitch == $3) {
n102=    unset %streamer. [ $+ [ # ] $+ ] .raid.streamer. [ $+ [ $2 ] ]
n103=    var %raid. [ $+ [ # ] ] $replace(%streamer. [ $+ [ # ] $+ ] .raid, @link@, https://www.twitch.tv/ , @raidtarget@, $$2)
n104=    .timerraid [ $+ [ # ] ] 4 0 msg # %raid. [ $+ [ # ] ]
n105=  }
n106=  elseif ((%streamer. [ $+ [ # ] $+ ] .raid.streamer. [ $+ [ $2 ] ] == beam) || ( $3 == beam)) {
n107=    set %streamer. [ $+ [ # ] $+ ] .raid.streamer. [ $+ [ $2 ] ] beam
n108=    var %raid. [ $+ [ # ] ] $replace(%streamer. [ $+ [ # ] $+ ] .raid, @link@, https://www.beam.pro/ , @raidtarget@, $$2)
n109=    .timerraid [ $+ [ # ] ] 4 0 msg # %raid. [ $+ [ # ] ]
n110=  }
n111=  else { 
n112=    var %raid. [ $+ [ # ] ] $replace(%streamer. [ $+ [ # ] $+ ] .raid, @link@, https://www.twitch.tv/ ,@raidtarget@, $2)
n113=    .timerraid [ $+ [ # ] ] 4 0 msg # %raid. [ $+ [ # ] ]
n114=  }
n115=}
n116=
n117=on *:text:*just subscribed*:#:{
n118=  if ($nick == twitchnotify) { 
n119=    var %sub. [ $+ [ # ] ] $replace(%streamer. [ $+ [ # ] $+ ] .sub,@sub@,$$1)
n120=    msg # %sub. [ $+ [ # ] ]
n121=  }
n122=}
n123=
n124=raw USERNOTICE:*:{
n125=  if (resub isin $msgtags) {
n126=    var %resub. [ $+ [ $1 ] ] $replace(%streamer. [ $+ [ $1 ] $+ ] .resub,@sub@,$msgtags(display-name).key,@month@,$msgtags(msg-param-months).key) 
n127=    msg $1 %resub. [ $+ [ $1 ] ]
n128=  }
n129=}
n130=
n131=on *:TEXT:!regular *:#:{
n132=  if ($msgtags(mod).key == 1 || $right(#,-1) == $nick)  {
n133=    if (add == $2) {
n134=      if (Regular. $+ # isin $level($3)) {
n135=        msg # /me $3 is already on the Regular List.
n136=      }
n137=      else {
n138=        auser -a Regular. [ $+ [ # ] ] $3
n139=      msg # /me $3 has been added to the Regular List. }
n140=    }
n141=    elseif (del == $2) {
n142=      if (Regular. $+ # isin $level($3)) {
n143=        ruser Regular. $+ # $3
n144=        msg # /me $3 has been removed from the Regular List.
n145=      }
n146=      else {
n147=        msg # /me $3 is not on the Regular List.
n148=      }
n149=    }
n150=  }
n151=}
n152=on *:text:!reloademotes:#:{
n153=  if (zapdos26 == $nick) {
n154=    /hfree -s allemotes
n155=    /hmake -s allemotes
n156=    /hload -s allemotes allemotes.txt
n157=    msg # Emotes have been reloaded!
n158=  }
n159=}
n160=
n161=
n162=on *:text:*:#:{
n163=  if ($msgtags(bits).key > 0) && ($msgtags(bits).key >= %streamer. [ $+ [ # ] $+ ] .bits.amount) {
n164=    set %bitsstreamermessage %streamer. [ $+ [ # ] $+ ] .bits.message
n165=    var %bitsstreamermessage $replace(%bitsstreamermessage, @bits@, $msgtags(bits).key, @nick@, $msgtags(display-name).key)
n166=    msg # %bitsstreamermessage
n167=  }
n168=  if ((%streamer. [ $+ [ # ] $+ ] .shoutout.command == $1) && ($msgtags(mod).key == 1 || $right(#,-1) == $nick)) {
n169=    if (twitch == $3) {
n170=      unset %streamer. [ $+ [ # ] $+ ] .shoutout.streamer. [ $+ [ $2 ] ]
n171=      var %shoutout $replace(%streamer. [ $+ [ # ] $+ ] .shoutout.message, @link@, https://www.twitch.tv/ , @nick@, $2)
n172=      msg # %shoutout
n173=    }
n174=    elseif ((%streamer. [ $+ [ # ] $+ ] .shoutout.streamer. [ $+ [ $2 ] ] == beam) || ($3 == beam)) {
n175=      set %streamer. [ $+ [ # ] $+ ] .shoutout.streamer. [ $+ [ $2 ] ] beam
n176=      var %shoutout $replace(%streamer. [ $+ [ # ] $+ ] .shoutout.message, @link@, https://www.beam.pro/ , @nick@, $2)
n177=      msg # %shoutout
n178=    }
n179=    else {
n180=      var %shoutout $replace(%streamer. [ $+ [ # ] $+ ] .shoutout.message, @link@, https://www.twitch.tv/ , @nick@, $2)
n181=      msg # %shoutout
n182=    }
n183=  }
n184=}
